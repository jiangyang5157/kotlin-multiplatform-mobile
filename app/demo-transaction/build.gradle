plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    defaultConfig {
        compileSdkVersion Config.CompileSdkVersion
        minSdkVersion Config.MinSdkVersion
        targetSdkVersion Config.TargetSdkVersion
        applicationId "com.gmail.jiangyang5157.demo_transaction"
        testInstrumentationRunner "com.gmail.jiangyang5157.demo_transaction.HiltTestRunner"
        versionCode Config.VersionCode
        versionName Config.VersionName

        flavorDimensions "data", "presentation"
    }

    productFlavors {
        dataA {
            dimension "data"
        }
        dataB {
            dimension "data"
        }
        presentationA {
            dimension "presentation"
        }
        presentationB {
            dimension "presentation"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = Config.KotlinJvmTarget
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
        lintConfig rootProject.file('lint.xml')
        htmlReport true
    }

    kapt {
        correctErrorTypes = true
    }

    testOptions {
        managedDevices {
            devices {
                googleApi31Pixel4(com.android.build.api.dsl.ManagedVirtualDevice) {
                    device = "Pixel 4"
                    apiLevel = 31
                    systemImageSource = "google"
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation Dep.Kotlin
    implementation Dep.Appcompat
    implementation Dep.Material

    testImplementation Dep.Junit

    androidTestImplementation Dep.AndroidxJunit
    androidTestImplementation Dep.EspressoCore
    androidTestImplementation Dep.TestRules

    // Internal
    implementation project(':transaction_domain_kt')
    dataAImplementation project(':transaction_data_1')
    dataBImplementation project(':transaction_data_2')
    implementation project(':transaction_presentation_base')
    presentationAImplementation project(':transaction_presentation_1')
    presentationBImplementation project(':transaction_presentation_2')

    // Dependency Injection
    implementation Dep.DaggerHilt
    kapt Dep.DaggerHiltCompiler
    kapt Dep.HiltCompiler
    androidTestImplementation Dep.DaggerHiltTest
    kaptAndroidTest Dep.DaggerHiltCompiler
}
