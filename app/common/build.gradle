plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

afterEvaluate {
    publishing {
        task androidSourcesJar(type: Jar) {
            archiveClassifier.set('sources')
            from android.sourceSets.main.java.srcDirs
        }

        publications {
            release(MavenPublication) {
                groupId = "com.gmail.jiangyang5157"
                artifactId = "common"
                version = "1.0"

                from components.release

                artifact androidSourcesJar
            }
        }
    }
}

android {

    defaultConfig {
        compileSdkVersion Config.CompileSdkVersion
        minSdkVersion Config.MinSdkVersion
        targetSdkVersion Config.TargetSdkVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
        lintConfig rootProject.file('lint.xml')
        htmlReport true
    }

    testOptions {
        managedDevices {
            devices {
                googleApi31Pixel4(com.android.build.api.dsl.ManagedVirtualDevice) {
                    device = "Pixel 4"
                    apiLevel = 31
                    systemImageSource = "google"
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation Dep.Kotlin
    implementation Dep.Inject
    implementation Dep.Appcompat
    implementation Dep.Material
    implementation Dep.LifecycleRuntime
    implementation Dep.CoroutinesAndroid

    testImplementation Dep.Junit

    androidTestImplementation Dep.AndroidxJunit
    androidTestImplementation Dep.TestRunner

    // Network
    implementation Dep.Retrofit
    implementation Dep.RetrofitGson
}
